#+feature dynamic-literals

package main

Type :: struct {
	kind:            Type_Kind,
	compiled:        Compiled_Type,
	signed:          bool, // not sure if this is the best place or I should have a kind union a be done with it
	numeric_integer: bool,
	numeric_float:   bool,
	fields:          [dynamic]Struct_Field,
	size:            u64,
	elem_type:       ^Type,
	pointee_type:    ^Type, // Maybe not needed, could use elem_type, for now use a different field for clarity
}

Struct_Field :: struct {
	name:  string,
	type:  ^Type,
	index: int,
}


Compiled_Type :: union {
	TypeRef,
}

Type_Kind :: enum {
	Undefined,
	Error,
	Void,
	Bool,
	Untyped_Int,
	Untyped_Float,
	Uint8,
	Uint16,
	Uint64,
	Uint32,
	Int8,
	Int16,
	Int32,
	Int64,
	Float16,
	Float32,
	Float64,
	String,
	Struct,
	Array,
	Pointer,
}

create_type :: proc(
	kind: Type_Kind,
	type_name: string,
	scope: ^Scope,
	signed := false,
	numeric_integer := false,
	numeric_float := false,
) {
	t := new(Type)
	t.kind = kind
	t.numeric_integer = numeric_integer
	t.numeric_float = numeric_float
	t.signed = signed
	scope.symbols[type_name] = make_symbol(.Type, t)
}

create_primitive_types :: proc(scope: ^Scope) {
	// NOTE: I don't know if void to be equivalent to empty type_expr is a good idea
	create_type(.Void, "", scope)
	create_type(.Bool, "bool", scope)

	create_type(.Untyped_Int, "untyped_int", scope)
	create_type(.Untyped_Float, "untyped_float", scope)
	create_type(.Uint8, "u8", scope, numeric_integer = true)
	create_type(.Uint16, "u16", scope, numeric_integer = true)
	create_type(.Uint32, "u32", scope, numeric_integer = true)
	create_type(.Uint64, "u64", scope, numeric_integer = true)
	create_type(.Int8, "i8", scope, signed = true, numeric_integer = true)
	create_type(.Int16, "i16", scope, signed = true, numeric_integer = true)
	create_type(.Int32, "i32", scope, signed = true, numeric_integer = true)
	create_type(.Int64, "i64", scope, signed = true, numeric_integer = true)
	create_type(.Float16, "f16", scope, signed = true, numeric_float = true)
	create_type(.Float32, "f32", scope, signed = true, numeric_float = true)
	create_type(.Float64, "f64", scope, signed = true, numeric_float = true)

	create_type(.String, "str", scope)
}

type_coercion :: proc(from: ^Type, to: ^Type, scope: ^Scope) -> ^Type {
	if from.kind == .Array && to.kind == .Array {
		if from.size == to.size && type_coercion(from.elem_type, to.elem_type, scope) != nil {
			return to
		}
	}

	if from.kind == .Untyped_Int && to.numeric_integer {
		return to
	}

	if to.kind == .Untyped_Int && from.numeric_integer {
		return from
	}

	if to.kind == .Untyped_Int && from.kind == .Untyped_Int {
		sym, _ := resolve_symbol(scope, "i64")
		return sym.type
	}

	if from.kind == .Untyped_Float && to.numeric_float {
		return to
	}

	if to.kind == .Untyped_Float && from.numeric_float {
		return from
	}

	if to.kind == .Untyped_Float && from.kind == .Untyped_Float {
		sym, _ := resolve_symbol(scope, "f64")
		return sym.type
	}

	if from.kind == to.kind {
		return from
	}

	return nil
}

// Set the type on an expression and propagate it inward to untyped sub-expressions.
// This replaces the need for separate coerce_array_elements / coerce_unary_inner calls.
set_expr_type :: proc(expr: ^Expr, type: ^Type, scope: ^Scope) {
	expr.type = type
	#partial switch &e in expr.data {
	case Expr_Unary:
		coerced := type_coercion(e.expr.type, type, scope)
		if coerced != nil {
			set_expr_type(e.expr, coerced, scope)
		}
	case Expr_Array_Literal:
		if type.elem_type == nil { return }
		for elem in e.elements {
			if elem.type.kind == .Array {
				set_expr_type(elem, type.elem_type, scope)
			} else {
				coerced := type_coercion(elem.type, type.elem_type, scope)
				if coerced != nil { elem.type = coerced }
			}
		}
	}
}

resolve_type_expr :: proc(type_expr: ^Type_Expr, scope: ^Scope) -> ^Type {
	switch te in type_expr {
	case string:
		sym, ok := resolve_symbol(scope, te)
		if !ok {
			return &error_type
		}
		return sym.type

	case Type_Expr_Array:
		elem_type := resolve_type_expr(te.elem, scope)
		if elem_type == &error_type {
			return &error_type
		}
		type := new(Type)
		type.kind = .Array
		type.size = te.size
		type.elem_type = elem_type

		return type

	case Type_Expr_Pointer:
		pointee_type := resolve_type_expr(te.pointee, scope)
		if pointee_type == &error_type {
			return &error_type
		}
		type := new(Type)
		type.kind = .Pointer
		type.pointee_type = pointee_type

		return type
	}
	return nil
}
