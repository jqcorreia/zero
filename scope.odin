package main

Symbol :: struct {
	name:  string,
	kind:  Symbol_Kind,
	type:  ^Type,
	decl:  ^Ast_Node,
	scope: ^Scope,
}

Symbol_Kind :: enum {
	Variable,
	Function,
	Type,
	Param,
	Field,
}

Symbol_Table :: map[string]^Symbol

Scope :: struct {
	kind:     ScopeKind,
	symbols:  Symbol_Table,
	function: ^Symbol,
	parent:   ^Scope,
}

ScopeKind :: enum {
	Global,
	Function,
	Block,
	Loop,
}

create_global_scope :: proc() -> ^Scope {
	scope := new(Scope)
	scope.kind = .Global
	create_primitive_types(scope)
	return scope
}

bind_scopes :: proc(node: ^Ast_Node, cur_scope: ^Scope) {
	node.scope = cur_scope
	#partial switch &data in node.data {
	case Ast_Block:
		for s in data.statements {
			bind_scopes(s, cur_scope)
		}
	case Ast_Var_Decl:
		_, ok := resolve_symbol(cur_scope, data.name)
		if !ok {
			sym := make_symbol(.Variable)
			cur_scope.symbols[data.name] = sym
			data.symbol = sym
		} else {
			error_span(node.span, "Re-declaration of variable '%s'", data.name)
		}
	case Ast_Struct_Decl:
		_, ok := resolve_symbol(cur_scope, data.name)
		if !ok {
			type := new(Type)
			type.kind = .Struct
			type.fields = {}
			sym := make_symbol(.Type)
			sym.name = data.name
			sym.type = type
			cur_scope.symbols[data.name] = sym
			data.symbol = sym
			for &field, idx in data.fields {
				append(&sym.type.fields, Struct_Field{name = field.name, index = idx})
			}
		} else {
			error_span(node.span, "Re-declaration of struct '%s'", data.name)
		}
	case Ast_Function:
		new_scope := make_scope(.Function, parent = cur_scope)
		symbol := new(Symbol)
		symbol.name = data.name
		symbol.kind = .Function
		symbol.decl = node
		symbol.scope = cur_scope
		new_scope.function = symbol
		cur_scope.symbols[data.name] = symbol
		for &param in data.params {
			sym := make_symbol(.Param)
			sym.decl = node
			sym.name = param.name
			new_scope.symbols[param.name] = sym
			param.symbol = sym
		}
		data.symbol = symbol
		if !data.external {
			get_block_symbols(data.body, new_scope)
		}
	case Ast_If:
		new_scope_then := make_scope(.Block, parent = cur_scope)
		get_block_symbols(data.then_block, new_scope_then)
		if data.else_block != nil {
			new_scope_else := make_scope(.Block, parent = cur_scope)
			get_block_symbols(data.else_block, new_scope_else)
		}
	case Ast_For:
		new_scope := make_scope(.Loop, parent = cur_scope)
		get_block_symbols(data.body, new_scope)
	}
}

get_block_symbols :: proc(s: ^Ast_Block, scope: ^Scope) {
	for node in s.statements {
		bind_scopes(node, scope)
	}
}

make_scope :: proc(kind: ScopeKind, parent: ^Scope) -> ^Scope {
	scope := new(Scope)
	scope.kind = kind
	scope.parent = parent
	return scope
}

make_symbol :: proc(kind: Symbol_Kind, type: ^Type = nil) -> ^Symbol {
	sym := new(Symbol)
	sym.kind = kind
	sym.type = type
	return sym
}

resolve_symbol :: proc(current_scope: ^Scope, name: string) -> (^Symbol, bool) {
	scope := current_scope
	for {
		if sym, ok := scope.symbols[name]; ok {
			return sym, true
		}
		if scope.parent == nil {
			break
		}
		scope = scope.parent
	}
	return nil, false
}

get_scope_function :: proc(scope: ^Scope) -> ^Symbol {
	for cur := scope; cur.parent != nil; cur = cur.parent {
		if cur.function != nil {
			return cur.function
		}
	}
	return nil
}
